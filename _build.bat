@ECHO OFF
CLS
ECHO Apply BuildCSharp bat file in SolARPipelineManager folder

::PUSHD ..\..\SolARPipelineManager\
::CALL BuildCSharp.bat
::POPD

SETLOCAL ENABLEDELAYEDEXPANSION

SET LANG=csharp
SET COMPILER=win-cl-14.1
SET SOLAR_WRAPPER_VERSION=0.9.0
SET SOLAR_VERSION=0.9.0
SET XPCF_VERSION=2.5.0
SET OUT=%REMAKEN_PKG_ROOT%\packages\SolARBuild\%COMPILER%\SolARWrapper\%SOLAR_WRAPPER_VERSION%\%LANG%

ECHO ----------------------- SWIG wrapping ----------------------------

SET OPTIONS=^
 -c++ ^
 -%LANG% ^
 -small -O ^
 -outcurrentdir ^
 -I./Swig ^
 -I./Swig/include ^
 -I"%REMAKEN_PKG_ROOT%/packages/%COMPILER%/xpcf/%XPCF_VERSION%/interfaces" ^
 -I"%REMAKEN_PKG_ROOT%/packages/SolARBuild/%COMPILER%/SolARFramework/%SOLAR_VERSION%/interfaces" ^
 -DXPCF_USE_BOOST ^
 -dllimport SolARWrapper ^
 

FOR %%F IN (Swig\*.i) DO (
ECHO ##########
ECHO # %%F
SET F=%%~nF
SET OUTPUT=%OUT%\!F:_=\!
IF NOT EXIST "!OUTPUT!" MKDIR "!OUTPUT!"
DEL /Q "!OUTPUT!\*.*"
CALL SWIG ^
 %OPTIONS% ^
 -namespace !F:_=.! ^
 -outdir "!OUTPUT!" ^
 %%F
)

ECHO ----------------------- SWIG completed ----------------------------

:: MonoPInvokeCallback should be added to static method generated by SWIG while using IL2CPP
ECHO ---------------- Edit for Android support ----------------------
ECHO DIR : %OUT%
PUSHD
CD /D %OUT%
::POWERSHELL -command "Get-ChildItem -Path .\ -Filter *PINVOKE.cs -Recurse -File -Name | ForEach-Object {echo """# $_""";[System.IO.File]::WriteAllText($_,([System.IO.File]::ReadAllText($_) -replace 'static void SetPending','[AOT.MonoPInvokeCallback(typeof(ExceptionDelegate))] static void SetPending' -replace 'static string CreateString','[AOT.MonoPInvokeCallback(typeof(SWIGStringDelegate))] static string CreateString'))}"
POPD

@echo off
cls
SET CURRENTDIR=%cd%
echo Apply BuildCSharp bat file in SolARPipelineManager folder

cd ..\..\SolARPipelineManager\
call BuildCSharp.bat
cd %CURRENTDIR%

setlocal ENABLEDELAYEDEXPANSION

SET LANG=csharp
SET COMPILER=win-cl-14.1
SET SOLAR_WRAPPER_VERSION=1.0.0
SET SOLAR_VERSION=1.0.0
SET XPCF_VERSION=2.6.2
SET OUT=%XPCF_MODULE_ROOT\SolARBuild\SolARWrapper\%SOLAR_WRAPPER_VERSION%\%LANG%

echo ----------------------- SWIG wrapping ----------------------------

SET OPTIONS=^
 -c++ ^
 -%LANG% ^
 -small -O ^
 -outcurrentdir ^
 -I./Swig ^
 -I./Swig/include ^
 -I%XPCF_MODULE_ROOT%/xpcf/%XPCF_VERSION%/interfaces ^
 -I%XPCF_MODULE_ROOT%/SolARBuild/SolARFramework/%SOLAR_VERSION%/interfaces ^
 -DXPCF_USE_BOOST ^
 -dllimport SolARWrapper ^
 

FOR %%F IN (Swig\*.i) DO (
ECHO ##########
ECHO # %%F
SET F=%%~nF
SET OUTPUT=%OUT%\!F:_=\!
IF NOT EXIST "!OUTPUT!" MKDIR "!OUTPUT!"
DEL /Q "!OUTPUT!\*.*"
CALL SWIG ^
 %OPTIONS% ^
 -namespace !F:_=.! ^
 -outdir "!OUTPUT!" ^
 %%F
)

GOTO :END

:END
echo ----------------------- SWIG completed ----------------------------

:: MonoPInvokeCallback should be added to static method generated by SWIG while using IL2CPP
echo ---------------- Edit for Android support ----------------------
echo DIR : %OUT%
cd /D %OUT%
powershell -command "Get-ChildItem -Path .\ -Filter *PINVOKE.cs -Recurse -File -Name | ForEach-Object {echo """# $_""";[System.IO.File]::WriteAllText($_,([System.IO.File]::ReadAllText($_) -replace 'static void SetPending','[AOT.MonoPInvokeCallback(typeof(ExceptionDelegate))] static void SetPending' -replace 'static string CreateString','[AOT.MonoPInvokeCallback(typeof(SWIGStringDelegate))] static string CreateString'))}"
cd /D %CURRENTDIR%
